plugins {
    id 'java'
    id 'com.github.spacialcircumstances.gradle-cucumber-reporting' version '0.1.17'
}

repositories {
    mavenCentral()
}

configurations {
    cucumberRuntime {
        extendsFrom testImplementation
    }
}

def getVersion() {
    def versionNumber = '1.0.5'
    println versionNumber
    versionNumber
}

cucumberReports {
    outputDir = file('build/cucumber/html')
    reports = files('build/cucumber/json/results.json')
    classifications = [
            // lazy evaluation - not during gradle configuration phase
            Version: "${-> getVersion()}"
    ]
}

task cucumber(type: Test) {
    dependsOn assemble, compileTestJava
    doLast {
        javaexec {
            main = 'io.cucumber.core.cli.Main'
            classpath = configurations.cucumberRuntime + sourceSets.main.output + sourceSets.test.output
            args = ['--plugin', 'pretty',
                    '--plugin', 'json:build/cucumber/json/results.json',
                    '--strict',
                    '--tags', 'not @ignore',
                    '--glue', 'uk.co.automatictester.spring.cucumber.step',
                    'src/test/resources/features']
        }
    }
}

test.finalizedBy(cucumber)

def lombokVersion = '1.18.12'
def springFrameworkVersion = '5.2.3.RELEASE'
def slf4jVersion = '1.7.30'
def cucumberVersion = '5.3.0'

dependencies {
    testImplementation "org.slf4j:slf4j-simple:${slf4jVersion}"
    testImplementation "org.slf4j:slf4j-api:${slf4jVersion}"
    testImplementation 'org.assertj:assertj-core:3.15.0'
    testImplementation "io.cucumber:cucumber-java8:${cucumberVersion}"
    testImplementation "io.cucumber:cucumber-spring:${cucumberVersion}"
    testImplementation "org.springframework:spring-context:${springFrameworkVersion}"
    testImplementation "org.springframework:spring-test:${springFrameworkVersion}"
    testImplementation "javax.annotation:javax.annotation-api:1.3.2"
    testCompileOnly "org.projectlombok:lombok:${lombokVersion}"
    testAnnotationProcessor "org.projectlombok:lombok:${lombokVersion}"
}
