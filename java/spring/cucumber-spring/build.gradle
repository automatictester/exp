apply plugin: 'java'

repositories {
    mavenCentral()
}

configurations {
    cucumberRuntime {
        extendsFrom testRuntime
    }
}

task cucumber() {
    dependsOn assemble, compileTestJava
    doLast {
        javaexec {
            main = "io.cucumber.core.cli.Main"
            classpath = configurations.cucumberRuntime + sourceSets.main.output + sourceSets.test.output
            args = ['--plugin', 'pretty',
                    '--strict',
                    '--tags', 'not @ignore',
                    '--glue', 'uk.co.automatictester.spring.cucumber.step',
                    'src/test/resources/features']
        }
    }
}

test.finalizedBy(cucumber)

def lombokVersion = '1.18.12'
def springFrameworkVersion = "5.2.3.RELEASE"
def slf4jVersion = '1.7.30'
def cucumberVersion = '5.3.0'

dependencies {
    testCompile "org.slf4j:slf4j-simple:${slf4jVersion}"
    testCompile "org.slf4j:slf4j-api:${slf4jVersion}"
    testCompile 'org.assertj:assertj-core:3.15.0'
    testCompile "io.cucumber:cucumber-java8:${cucumberVersion}"
    testCompile "io.cucumber:cucumber-spring:${cucumberVersion}"
    testCompile "org.springframework:spring-context:${springFrameworkVersion}"
    testCompile "org.springframework:spring-test:${springFrameworkVersion}"
    testCompile "javax.annotation:javax.annotation-api:1.3.2"
    testCompileOnly "org.projectlombok:lombok:${lombokVersion}"
    testAnnotationProcessor "org.projectlombok:lombok:${lombokVersion}"
}
